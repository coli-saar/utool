
options {
  JDK_VERSION = "1.5";
    LOOKAHEAD = 2;
  UNICODE_INPUT = true;
}

PARSER_BEGIN(RewritingSystemParser)
package de.saar.chorus.domgraph.chart.lethal;

import java.io.*;
import java.util.*;
import de.saar.chorus.domgraph.chart.*;

public class RewritingSystemParser {
    private static RewriteSystem weakening, equivalence;
    private static Annotator annotator;

    public RewritingSystemParser() {
		this((Reader) null);
    }

    public void read(Reader reader, RewriteSystem weakening, RewriteSystem equivalence, Annotator annotator) throws Exception {
    	ReInit(reader);

        this.weakening = weakening;
        this.annotator = annotator;
	Input();

//	    return grammar;
    }


}
PARSER_END(RewritingSystemParser)

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
}




TOKEN :
{
    <STARTANN  : "start annotation">
 |  <NEUTRALANN : "neutral annotation">
 |  <NUMBER : (["0"-"9"])+>
 |  <ID   : ["a"-"z"] (["a"-"z", "A"-"Z", "0"-"9", "_", "\'", "&", "+", "-"])*>
 |  <OTHER: ["A"-"Z", "_", "\'", "&", "+", "-"] (["a"-"z", "A"-"Z", "0"-"9", "_", "\'", "&", "+", "-"])*>
 | <LINE_END_COMMENT: "//" (~["\n"])* >

}

void Input() : {
}
{
    (<LINE_END_COMMENT> | WeakeningRule() | EquivalenceRule() | AnnotatorComponent())*
}


void WeakeningRule(): {
    String ann;
    String f1,f2;
    int n1,n2;
}{
    "[" ann=Annotation() "]" f1=<ID>.image "/" n1=Number() ">" f2=<ID>.image "/" n2=Number() {
        weakening.addRule(f1,n1,f2,n2,ann);
        //System.err.println(ann + ":" + f1 + n1 + " - " + f2 + n2);
    }
}

int Number(): {
    String x;
}{
    x=<NUMBER>.image  { return Integer.parseInt(x); }
}

void EquivalenceRule(): {
    String f1,f2;
    int n1,n2;
}{
    (f1=<ID>.image "/" n1=Number() "=" f2=<ID>.image "/" n2=Number()) {
        System.err.println(f1 + n1 + " = " + f2 + n2);
    }

    | ("*" "=" f1=<ID>.image "/" n1=Number()) {
        System.err.println(f1 + n1 + " = wildcard");
    }
}


void AnnotatorComponent(): {
    String x,y,f;
    List<String> childAnnotations = new ArrayList<String>();
}{
    <STARTANN> ":" x=Annotation() { annotator.setStartAnnotation(x); }
    | <NEUTRALANN> ":" x=Annotation() { annotator.setNeutralAnnotation(x); }
    | (x=Annotation() ":" f=<ID>.image "("
        y=Annotation() { childAnnotations.add(y); } ("," y=Annotation() { childAnnotations.add(y); })* ")") {
      annotator.addRule(x,f,childAnnotations);
     }
}

String Annotation(): {
    String x;
}{
    (x=<ID>.image | x=<NUMBER>.image | x=<OTHER>.image) {
        return x;
    }
}